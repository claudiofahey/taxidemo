plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "application"

group = "io.pravega.example.taxidemo"
version = samplesVersion
archivesBaseName = "taxidemo-flinkprocessor"
description = "flinkprocessor"
mainClassName = "io.pravega.example.taxidemo.flinkprocessor.ApplicationMain"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repository.apache.org/snapshots"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    // Note that we must include the Elasticsearch client early in the classpath to avoid Netty version conflicts.
    // org.apache.flink:flink-runtime_2.11:1.3.1 includes io.netty:netty-all:4.0.27.Final which is too old.
    compile "org.elasticsearch.client:transport:5.1.2"
    compile "org.slf4j:slf4j-api:1.6.6"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "ch.qos.logback:logback-core:1.2.3"
    compile "net.logstash.logback:logstash-logback-encoder:4.11"
    compile "io.pravega:pravega-connectors-flink_2.11:${pravegaFlinkConnectorVersion}"
    compile "org.apache.flink:flink-streaming-java_2.11:${flinkVersion}"
    compile "org.apache.flink:flink-streaming-scala_2.11:${flinkVersion}"
    compile "org.apache.flink:flink-ml_2.11:${flinkVersion}"
    compile "org.apache.flink:flink-connector-elasticsearch5_2.11:${flinkVersion}"
    compile "io.pravega:pravega-client:${pravegaVersion}"

    // common-library dependencies
    compile "com.google.guava:guava:20.0"
    compile "org.xerial:sqlite-jdbc:3.20.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.1"

    testCompile "org.apache.flink:flink-test-utils_2.11:${flinkVersion}"
    testCompile group: "junit", name: "junit", version: "4.11"
}

shadowJar {
    dependencies {
        exclude(dependency("org.scala-lang:scala-library"))
        exclude(dependency("org.scala-lang:scala-reflect"))
        exclude(dependency("org.scala-lang:scala-compiler"))
    }

    relocate "com.google", "com.dell.nautilus.analytics.shaded.com.google"
    relocate "io.netty", "com.dell.nautilus.analytics.shaded.io.netty"

    zip64 true
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into("lib") {
                from shadowJar
                from(project.configurations.shadow)
            }
        }
    }
}
